mapscripts BlackVoid_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [VAR_TEMP_0, 0: BlackVoid_EventScript_WarpIntoMap]
    MAP_SCRIPT_ON_FRAME_TABLE 
    [
        VAR_TEMP_0, 0: BlackVoid_EventScript_OnFrameTable0
        VAR_TEMP_2, 1: BlackVoid_EventScript_OnFrameTable1
    ]
}

//BlackVoid_EventScript_

script BlackVoid_EventScript_OnFrameTable0 {
    if(var(VAR_TEMP_1 == 0))
    {
        lock
        playbgm(MUS_ABANDONED_SHIP, FALSE)
    }
    if(var(VAR_TEMP_1) >= 60)
    {
        setvar(VAR_TEMP_0, 1)
        setvar(VAR_TEMP_2, 1)
    }
    addvar(VAR_TEMP_1, 1)
    end
}

script BlackVoid_EventScript_OnFrameTable1 {
    setvar(VAR_TEMP_2, 0)
    message(BlackVoid_Text_Intro)
    waitmessage

    message(BlackVoid_Text_WhereAreYouFrom)
    waitmessage
    multichoice(25, 5, MULTI_ORIGIN_REGION, TRUE)
    if(var(VAR_RESULT) == 0)
    {
        setvar(VAR_START_QUESTION_1, 0)
        message(BlackVoid_Text_Comment_Kanto)
    }
    elif(var(VAR_RESULT) == 1)
    {
        setvar(VAR_START_QUESTION_1, 1)
        message(BlackVoid_Text_Comment_Johto)
    }
    elif(var(VAR_RESULT) == 2)
    {
        setvar(VAR_START_QUESTION_1, 2)
        message(BlackVoid_Text_Comment_Hoenn)
    }
    waitmessage
    

    message(BlackVoid_Text_HowAreYouFeeling)
    waitmessage
    multichoice(25, 5, MULTI_FEELING, TRUE)
    if(var(VAR_RESULT) == 0)
    {
        setvar(VAR_START_QUESTION_2, 0)
        message(BlackVoid_Text_Comment_Calm)
    }
    elif(var(VAR_RESULT) == 1)
    {
        setvar(VAR_START_QUESTION_2, 1)
        message(BlackVoid_Text_Comment_Excited)
    }
    elif(var(VAR_RESULT) == 2)
    {
        setvar(VAR_START_QUESTION_2, 2)
        message(BlackVoid_Text_Comment_Joyful)
    }
    waitmessage
    

    message(BlackVoid_Text_WhatIsMotivation)
    waitmessage
    multichoice(25, 5, MULTI_MOTIVATION, TRUE)
    if(var(VAR_RESULT) == 0)
    {
        setvar(VAR_START_QUESTION_3, 0)
        message(BlackVoid_Text_Comment_Power)
    }
    elif(var(VAR_RESULT) == 1)
    {
        setvar(VAR_START_QUESTION_3, 1)
        message(BlackVoid_Text_Comment_Adventure)
    }
    elif(var(VAR_RESULT) == 2)
    {
        setvar(VAR_START_QUESTION_3, 2)
        message(BlackVoid_Text_Comment_Mysteries)
    }
    waitmessage

    message(BlackVoid_Text_Outro)
    waitmessage
    closemessage

    fadescreen(FADE_TO_BLACK)
    warpsilent(MAP_MYSTERY_CAVE, 33, 5)
    delay(10)
    fadescreen(FADE_FROM_BLACK)
    setrespawn(HEAL_LOCATION_MYSTERY_CAVE)

    release

    end
}

script BlackVoid_EventScript_WarpIntoMap {
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_BLACK_VOID)
    end
}

text BlackVoid_Text_Intro {
    format("To save the world your existence has spontaneously come into being.\pWhen creating you our god had asked himself various questions, like...\p")
}

text BlackVoid_Text_Outro {
    format("Be on your way to save this timeline.\pMy servant will await you at the end of the time tunnel.\p")
}


text BlackVoid_Text_WhereAreYouFrom {
    format("Where are you from?")
}

text BlackVoid_Text_HowAreYouFeeling {
    format("How are you feeling right now?")
}

text BlackVoid_Text_WhatIsMotivation {
    format("What is your motivation?")
}

text BlackVoid_Text_Comment_Kanto {
    format("A region rich in history and tradition, Kanto is home to some of the most iconic creatures and towns.\pKnown for its diversity in geography, from forests to caves, it hosts the first Pokémon League in the series.\p")
}

text BlackVoid_Text_Comment_Johto {
    format("A region steeped in mythology and ancient lore.\pIt features serene landscapes and a strong connection to nature, with landmarks like the Bell Tower and Ruins of Alph.\p")
}

text BlackVoid_Text_Comment_Hoenn {
    format("A tropical region with a variety of climates, Hoenn is characterized by its islands, rainforests, and volcanoes.\pIts unique terrain promotes environmental themes, and it boasts legendary Pokémon tied to land, sea, and sky.\p")
}

text BlackVoid_Text_Comment_Calm {
    format("Composed and serene, maintaining a sense of peace and control, even in stressful or uncertain situations.\p")
}

text BlackVoid_Text_Comment_Excited {
    format("Enthusiastic and full of energy, often eager and looking forward to upcoming events or experiences.\p")
}

text BlackVoid_Text_Comment_Joyful {
    format("Exuding happiness and delight, often radiating positivity and contentment in their interactions and outlook on life.\p")
}

text BlackVoid_Text_Comment_Power {
    format("I see you are driven by strength, confidenence, and control over your actions.\p")
}

text BlackVoid_Text_Comment_Adventure {
    format("I see you are curios and have a desire for exploration, thriving on new experiences and the excitement of the unknown.\p")
}

text BlackVoid_Text_Comment_Mysteries {
    format("I see you are intrigued by the unknown, fascinated by solving puzzles, uncovering hidden truths, or exploring the enigmatic aspects of life.\p")
}