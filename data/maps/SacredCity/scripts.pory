const LOCAL_FLYING_MACHINE = 1

mapscripts SacredCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SacredCity_OnTransition
    MAP_SCRIPT_ON_RESUME: SacredCity_OnResume
    MAP_SCRIPT_ON_LOAD: SacredCity_OnLoad
}

script SacredCity_OnTransition {
    setflag(FLAG_VISITED_SACRED_CITY)
    end
}

script SacredCity_OnResume {
    setdivewarp(MAP_UNDERWATER_SACRED_CITY, 9, 6)
    end
}

script SacredCity_OnLoad {
    call_if_unset(FLAG_MOUNTAIN_PATH_FREE, SacredCity_Hide_Cave_Entrance)
    end
}

script SacredCity_Hide_Cave_Entrance {
    setmetatile(25, 6, METATILE_Sootopolis_Sootopolis_RockWall, TRUE)
    setmetatile(25, 7, METATILE_Sootopolis_Sootopolis_RockWall, TRUE)
    end
}

script SacredCity_EventScript_FlyMachine {
    removeobject(LOCAL_FLYING_MACHINE)
    setflag(FLAG_ITEM_FLYING_MACHINE)
    setflag(FLAG_PLAYER_CAN_USE_FLY)
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(SacredCity_Text_FlyMachine)
    waitfanfare
    waitbuttonpress
    closemessage
}

script SacredCity_EventScript_Artist {
    lock
    faceplayer
    if(!flag(FLAG_DEFEATED_GROUDON))
    {
        msgbox(format("Rumors say, that the deadly drought on northern part of the island is because of a powerful Pok√©mon hybernating there.\pBut where would such a majestic beast be?"), MSGBOX_NPC)
    }
    else
    {
        msgbox(format("They say the mysterious weather on on northern part of the island has stopped as suddenly as it had begun."))
    }

    release
    end    
}

script SacredCity_EventScript_MountainGuardian{
    lock
    faceplayer
    message(format("There used to be a path here which led up the mountain. But I has vanished since KYOGRE raised the water level.\p"))
    waitmessage
    
    checkitem(ITEM_RED_ORB)
    if(var(VAR_RESULT) == TRUE && !flag(FLAG_RED_ORB_USED))
    {
        message(format("I see you are carrying the sacred RED ORB. But it seems to have lost its luster.\pMaybe charging it with some energy will restore it to its former glory.\p"))
        waitmessage
        closemessage
        release
        end
    }

    checkitem(ITEM_BLUE_ORB)
    if(var(VAR_RESULT) == TRUE && !flag(FLAG_BLUE_ORB_USED))
    {
        message(format("I see you are carrying the sacred BLUE ORB. But it seems to have lost its luster.\pMaybe charging it with some energy will restore it to its former glory.\p"))
        waitmessage
        closemessage
        release
        end
    }

    message(format("My grand father had once told me about a temple which could open the path to the mountain.\pAn ancient ritual involving two ORBs was once used to please the god and gain entrance to the mountain.\p"))
    waitmessage
    closemessage
    release
    end
}



text SacredCity_Text_FlyMachine {
    format("Obtained FLY MACHINE!")
}
