const FISHERMAN_ID = 1
const LOCALID_ZAPDOS = 2

mapscripts Route201_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Route201_EventScript_OnTransition
}

script Route201_EventScript_OnTransition {
    if(!flag(FLAG_ANABEL_HIDE_AND_SEEK) || flag(FLAG_ZAPDOS_FOUND))
    {
        call(Route201_EventScript_HideZapdos)
    }
    end
}

script Route201_EventScript_Fisherman {
    lock

    call_if_unset(FLAG_PLAYER_CAN_USE_SURF, FishermanGiveSharpedoMachine)
    call_if_set(FLAG_PLAYER_CAN_USE_SURF, FishermanGeneric)
    end
}

script FishermanGiveSharpedoMachine {
    applymovement(FISHERMAN_ID, FishermanFacePlayerMovement)
    waitmovement(0)
    msgbox(FishermanText, MSGBOX_DEFAULT)

    playfanfare(MUS_OBTAIN_ITEM)
    setflag(FLAG_PLAYER_CAN_USE_SURF)
    giveitem(ITEM_MACHINE_SURF)
    waitfanfare
    applymovement(FISHERMAN_ID, FishermanFaceWaterMovement)
    waitmovement(0)
    
    release
    end
}

script FishermanGeneric {
    msgbox(FishermanGenericText, MSGBOX_DEFAULT)
    release
    end
}

script Route201_EventScript_HideZapdos {
    setflag(FLAG_TEMP_1)
    removeobject(LOCALID_ZAPDOS)
    return
}

script Route201_EventScript_Zapdos {
    lock
    playmoncry(SPECIES_ZAPDOS, CRY_MODE_ENCOUNTER)
    waitmoncry
    setfieldeffectargument(0, LOCALID_ZAPDOS)
    dofieldeffect(FLDEFF_NPCFLY_OUT)
    delay(15)
    call(Route201_EventScript_HideZapdos)
    addvar(VAR_HIDE_AND_SEEK, 1)
    setflag(FLAG_ZAPDOS_FOUND)
    release
    end
}

text FishermanText {
    format("You want to surf on your what?!\nThat is cruelty!\lHow barbaric.\pThese days we use MACHINES.\lHere you can have my SURF MACHINE.")
}

text SharpedoMachineObtained {
    format("SHARPEDO MACHINE obtained.")
}

text FishermanGenericText {
    format("I am all out of SURF MACHINES.\nHow am I gonna get home?")
}

movement FishermanFacePlayerMovement {
    face_player    
}

movement FishermanFaceWaterMovement {
    face_right    
}