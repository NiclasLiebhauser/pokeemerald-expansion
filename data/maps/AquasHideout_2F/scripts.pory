const LOCAL_DIVE_MACHINE = 1
const LOCAL_ARCHIE = 2
const LOCAL_DIVE_GRUNT = 3
const LOCALID_AQUA_GRUNT_1 = 11
const LOCALID_AQUA_GRUNT_2 = 10
const LOCALID_AQUA_GRUNT_3 = 9
const LOCALID_AQUA_GRUNT_4 = 8

mapscripts AquasHideout_2F_MapScripts {
    MAP_SCRIPT_ON_LOAD: AquasHideout_2F_EventScript_OnLoad
}

script AquasHideout_2F_EventScript_OnLoad {
    if(flag(FLAG_PLAYER_CAN_USE_DIVE))
    {
        setvar(VAR_TEMP_0, 1)
    }
}

script AquasHideout_2F_EventScript_DiveMachine {
    setflag(FLAG_ITEM_DIVE_MACHINE)
    playfanfare(MUS_OBTAIN_ITEM)
    setflag(FLAG_PLAYER_CAN_USE_DIVE)
    giveitem(ITEM_MACHINE_DIVE)
    waitfanfare
    waitmessage
    closemessage
    return
}

script AquasHideout_2F_EventScript_EnterLab_Up {
    lock
    call(AquasHideout_2F_DiveGrunt_FaceDown)
    call(AquasHideout_2F_DiveGrunt_Cutscene)
}

script AquasHideout_2F_EventScript_EnterLab_Down {
    lock
    setflag(FLAG_TEMP_1)
    call(AquasHideout_2F_DiveGrunt_FaceDown)
    applymovement(LOCAL_DIVE_GRUNT, Common_Movement_WalkDown)
    waitmovement(0)
    call(AquasHideout_2F_DiveGrunt_Cutscene)
}

script AquasHideout_2F_DiveGrunt_Cutscene {
    applymovement(LOCAL_DIVE_GRUNT, AquasHideout_2F_Movement_DiveGrunt_WalkOn)
    waitmovement(0)

    msgbox(format("Are you the guy?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        message(format("Great! I was waiting for you.\p"))
    }
    else
    {
        message(format("No? Weird...\nAnyways I am looking for someone to test out my new DIVE MACHINE.\p"))
    }

    waitmessage
    message(format("Just a moment...\p"))
    waitmessage
    closemessage

    applymovement(LOCAL_DIVE_GRUNT, Common_Movement_WalkRight2)
    waitmovement(0)

    if(flag(FLAG_TEMP_1))
    {
        applymovement(LOCAL_DIVE_GRUNT, Common_Movement_WalkUp)
        waitmovement(0)
    }

    applymovement(LOCAL_DIVE_GRUNT, AquasHideout_2F_Movement_DiveGrunt_WalkItem)
    waitmovement(0)

    delay(30)
    removeobject(LOCAL_DIVE_MACHINE)
    playse(SE_BALL)
    waitse
    delay(15)

    if(flag(FLAG_TEMP_1))
    {
        applymovement(LOCAL_DIVE_GRUNT, Common_Movement_WalkDown)
        waitmovement(0)
    }

    applymovement(LOCAL_DIVE_GRUNT, AquasHideout_2F_Movement_DiveGrunt_WalkOn)
    waitmovement(0)

    call(AquasHideout_2F_EventScript_DiveMachine)

    applymovement(LOCAL_DIVE_GRUNT, Common_Movement_WalkRight7)
    waitmovement(0)

    removeobject(LOCAL_DIVE_GRUNT)

    setvar(VAR_TEMP_0, 1)

    release
    end
}

script AquasHideout_2F_DiveGrunt_FaceDown {
    playse(SE_PIN)
    applymovement(LOCAL_DIVE_GRUNT, Common_Movement_FaceDown)
    waitmovement(0)
    applymovement(LOCAL_DIVE_GRUNT, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCAL_DIVE_GRUNT, Common_Movement_Delay48)
    waitmovement(0)
    return    
}

script AquasHideout_2F_EventScript_Archie {
    lockall
    msgbox(AquasHideout_2F_Text_Archie_PreBattel)
    waitmessage
    closemessage
    trainerbattle_no_intro(TRAINER_ARCHIE_AQUA_HQ, AquasHideout_2F_Text_Archie_Defeat)
    msgbox(AquasHideout_2F_Text_Archie_PostBattel)
    giveitem(ITEM_BLUE_ORB_OFF)
    waitse
    waitmessage
    closemessage
    setflag(FLAG_AQUA_LEFT_HIDEOUT)
    call(AquasHideout_2F_RemoveAqua)
    setvar(VAR_LEVEL_CAP, 60)
    call(EventScript_Increase_LevelCap)
    releaseall
    end
}

script AquasHideout_2F_RemoveAqua {
    fadescreen(FADE_TO_BLACK)
    playse(SE_EXIT)
    waitse

    removeobject(LOCAL_ARCHIE)
    removeobject(LOCALID_AQUA_GRUNT_1)
    removeobject(LOCALID_AQUA_GRUNT_2)
    removeobject(LOCALID_AQUA_GRUNT_3)
    removeobject(LOCALID_AQUA_GRUNT_4)

    fadescreen(FADE_FROM_BLACK)
    return
}

script AquasHideout_2F_EventScript_Grunt1 {
    trainerbattle_single(TRAINER_AQUA_GRUNT_AQUAHQ_2F_1, AquasHideout_2F_Text_Grunt1_Intro, AquasHideout_2F_Text_Grunt1_Defeat)
    msgbox(AquasHideout_2F_Text_Grunt1_PostBattle, MSGBOX_AUTOCLOSE)
}

script AquasHideout_2F_EventScript_Grunt2 {
    trainerbattle_single(TRAINER_AQUA_GRUNT_AQUAHQ_2F_2, AquasHideout_2F_Text_Grunt2_Intro, AquasHideout_2F_Text_Grunt2_Defeat)
    msgbox(AquasHideout_2F_Text_Grunt2_PostBattle, MSGBOX_AUTOCLOSE)
}

script AquasHideout_2F_EventScript_Grunt3 {
    trainerbattle_single(TRAINER_AQUA_GRUNT_AQUAHQ_2F_3, AquasHideout_2F_Text_Grunt3_Intro, AquasHideout_2F_Text_Grunt3_Defeat)
    msgbox(AquasHideout_2F_Text_Grunt3_PostBattle, MSGBOX_AUTOCLOSE)
}

script AquasHideout_2F_EventScript_Grunt4 {
    trainerbattle_single(TRAINER_AQUA_GRUNT_AQUAHQ_2F_4, AquasHideout_2F_Text_Grunt4_Intro, AquasHideout_2F_Text_Grunt4_Defeat)
    msgbox(AquasHideout_2F_Text_Grunt4_PostBattle, MSGBOX_AUTOCLOSE)
}

text AquasHideout_2F_Text_Grunt1_Intro {
    format("You made it this far, but your journey ends here!")
}

text AquasHideout_2F_Text_Grunt1_Defeat {
    format("I can't believe I lost...")
}

text AquasHideout_2F_Text_Grunt1_PostBattle {
    format("You got lucky this time, but don’t think it’ll happen again!")
}

text AquasHideout_2F_Text_Grunt2_Intro {
    format("We’re not going to let you mess with our plans! Get ready!")
}

text AquasHideout_2F_Text_Grunt2_Defeat {
    format("No way... I was sure I had you.")
}

text AquasHideout_2F_Text_Grunt2_PostBattle {
    format("You won this round, but we’ll be back stronger!")
}

text AquasHideout_2F_Text_Grunt3_Intro {
    format("You think you can take us down? I’ll stop you right here!")
}

text AquasHideout_2F_Text_Grunt3_Defeat {
    format("This can’t be happening...")
}

text AquasHideout_2F_Text_Grunt3_PostBattle {
    format("I won’t forget this! Next time, you’re going down!")
}

text AquasHideout_2F_Text_Grunt4_Intro {
    format("You’re not leaving this floor without a fight! Let’s see what you’ve got!")
}

text AquasHideout_2F_Text_Grunt4_Defeat {
    format("Defeated... just like that?")
}

text AquasHideout_2F_Text_Grunt4_PostBattle {
    format("Don’t get too cocky. We’ll be ready for you next time!")
}

text AquasHideout_2F_Text_DiveMachine {
    format("Obtained DIVE MACHINE!")
}

text AquasHideout_2F_Text_Archie_PreBattel {
    format("It was all my fault. If he hadn't given me the BLUE ORB on that fate full day... then... maybe...\pBut what am I crying to you about that? I am Archie, the leader of the strongest team in all of Hoenn.\pWe achieved our goal of raising the sea level to improve life on Earth.\pExcept this whole situation makes me feel miserable.\pPlease end my pain.")
}

text AquasHideout_2F_Text_Archie_PostBattel {
    format("I think I will renounce my leadership. What good came of it?\pMaybe I will try and become a fisherman. I always loved the sea.")
}

text AquasHideout_2F_Text_Archie_Defeat {
    format("Serves me right...")
}

movement AquasHideout_2F_Movement_DiveGrunt_WalkOn {
    walk_down*3
    walk_left*2
}

movement AquasHideout_2F_Movement_DiveGrunt_WalkItem {
    walk_up*3
    face_left
}